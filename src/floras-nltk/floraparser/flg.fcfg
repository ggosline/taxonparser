%start Start

Start -> DESCRIPTION

CDELIM -> COMMA |

DESCRIPTION[subject=?s, chars=?clist] -> SUBJECT[H=?s] CDELIM CHARLIST[H=?clist]

SUBJECT[H=[orth=?o, mod=?m]] -> CBEGINNER AP[H=?m] N[category=structure, orth=?o]
SUBJECT[H=[orth=?o, mod=?m]] -> CBEGINNER AP[H=?m] N[category='life-form', orth=?o]
SUBJECT[H=[orth=?o, mod=?m]] -> CBEGINNER AP[H=?m] N[category='feature', orth=?o]
SUBJECT[H=[orth=?o]] -> CBEGINNER N[category=structure, orth=?o]
SUBJECT[H=[orth=?o]] -> CBEGINNER N[category=structure, orth=?o] OFP
SUBJECT[H=[orth=?o]] -> CBEGINNER N[category='life-form', orth=?o]
SUBJECT[H=[category=structure, orth=?o]] -> CBEGINNER UNK[orth=?o]

CHARLIST[H=(?h1+?h2)] -> CHAR[H=?h1] CHARLIST[H=?h2]
CHARLIST[H=?h] -> CHAR[H=?h]

CHAR[H=?h] -> CHRL[H=?h] CTERMINATOR
CHAR[category=?c, H=?h] -> WITHP[category=?c, H=?h]
CHAR[category=presence, H=?h] -> OR ABAR[category=presence, H=?h]
CHAR[category=presence, H=?h] -> ABAR[category=presence, H=?h] OR

CHAR[H=?h, stage=?st] -> STAGE[stage=?st] CHARLIST[H=?h]

CBEGINNER -> '¢'
CTERMINATOR -> COMMA | '$'
# CTERMINATOR ->

CHR[category=?c, H=?h, stage=?st] ->  CHR[category=?c, H=?h] STAGE[stage=?st]
CHR[category=?c, H=?h, stage=?st] ->  CHR[category=?c, H=?h] SUBSET[stage=?st]
CHR[category=?c, H=?h, posit=?p] ->  CHR[category=?c, H=?h] POSITION[posit=?p]
CHAR[category=?c, H=?h, posit=?p] ->  POSITION[posit=?p] CHARLIST[category=?c, H=?h]

CHR[category=?c, H=?h] ->  APL[category=?c, H=?h]
CHR[category=?c, H=?h] ->  AP[category=?c, H=?h]
CHR[category=?c, H=?h] ->  WITHP[category=?c, H=?h]

CHR[+anaph, category=?c, H=?h] -> ANAPH[category=?c] CHARLIST[H=?h]

CHR[category=?c, H=?h] -> ISA NPL[category=?c, H=?h]
ISA -> IS

CHR[H=?h, category=size] ->  SIZEB[H=?h]
CHR[H=?h, category=count] ->  COUNT[H=?h]

CHRL[H=?h, category=?c] ->  CHR[H=?h, category=?c]

NL[category={?c+?d}, H=(1)[orth=(?o+?ol)], orth=(1)] -> N[category=?d, orth = ?o] NL[category=?c, orth=?ol, H=?h2]
NL[category=?c, orth=?o, H=[orth=?o]] -> N[category=?c, orth=?o]

NBAR[category=?c, H=[orth=?h, mod=?m]] -> AP[H=?m] NL[category=?c, H=?h]
NBAR[category=?c, H=[orth=?h, count=?count]] -> COUNT[H=[val=?count]] NBAR[category=?c, H=?h]
NBAR[category=?c, H=?h] -> NL[category=?c, H=?h]
NBAR[category=?c, H=?h] -> NBAR[category=?c, H=?h] OFP
NBAR[category=?c, H=?h, mod=?m] -> NBAR[category=?c, H=?h] PP[H=?m]

NBAR[category=?c, H=?h] -> NBAR[category=?c, H=?h] PAREXP
NBAR[category=pro] -> PRO

NP[category=?c, H=?h] -> DET NBAR[category=?c, H=?h] | ART NBAR[category=?c, H=?h] | NBAR[category=?c, H=?h]

NP -> NAME
NP -> A[+compar] NBAR THAN S
NP -> A[+compar] NBAR THAN S/NBAR

NP -> ANAPH

NP[H=(?h1,'AND',?h2)] -> NP[H=?h1] AND NP[H=?h2]

NPL[H=(?h1+'OR'+?h2), category=?c] -> NP[H=?h1, category=?c] ORDELIM NPL[H=?h2, category=?c]
NPL[H=?h1, category=?c] -> NP[H=?h1, category=?c]
#NPL[H=(?h1+'OR'+?h2), category=?c, +OR] -> NPL[H=?h1, category=?c] CDELIM OR NP[H=?h2, category=?c]

BYX -> CONJ[conj='×'] | 'x'
SIZE[category=size, H=[category=size, num=?num, unit=?unit, dim=?dim]]  -> NUM[orth=?num] UNIT[orth=?unit] DIMEN[dim=?dim]
SIZE[category=size, H=[category=size, num=?num, unit=?unit, dim=long]]  -> NUM[orth=?num] UNIT[orth=?unit]
SIZE[category=size, H=[category=size, length=?num1, width=?num2, unit=?unit]]  -> NUM[orth=?num1] BYX NUM[orth=?num2] UNIT[orth=?unit]

SIZEB[category=size, H=(?h1+?h2)] -> SIZE[H=?h1] AND SIZE[H=?h2]
SIZEB[category=size, H=(?h1+?h2)] -> SIZE[H=?h1] COMMA SIZESIZE[H=?h2]
SIZEB[category=size, H=?h1] -> DEG SIZE[H=?h1]  |  SIZE[H=?h1]  | RANGE SIZE[H=?h1]
SIZEB[category=size, H=?h1] -> SIZEB[H=?h1] OR A[+compar]

DIMEN[dim=?dim] -> P[prep='in'] DIM[orth=?dim]
DIMEN[dim=?dim] -> DIM[orth=?dim]
COUNT[category=count, H=[+count, val=?num]] -> NUM[orth=?num]
COUNT[category=count, H=[+per, val=?num, obj=?h]] ->  NUM[orth=?num] P[prep='per'] NBAR[H=?h]
COUNT[category=count, H=[val=?num, group=?h]] -> NUM[orth=?num] N[+group, orth=?h]
COUNT[H=?h, mod=?deg] -> DEG[orth=?deg] COUNT[H=?h]
COUNT[H=?h, mod=?h] -> RANGE[orth=?deg] COUNT[H=?h]

ADVB[category=adv, orth=?o] -> ADV[orth=?o]
ADVB[category=degree, orth=?o] -> DEG[orth=?o]

ADVB[category=?c, orth=?o, mod=?m] -> DEG[orth=?m] ADVB[category=adv, orth=?o]
ADVB -> ADVB[category=adv, orth=?o] TO ADVB[category=adv, orth=?o]
ADVB[category=?c, orth=(?o1,?o2)] -> ADVB[category=?c, orth=?o1] ADVB[category=?c, orth=?o2]
ADVB[category=?c, orth=(?o1,TO,?o2)] -> ADVB[category=?c, orth=?o1] TO ADVB[category=?c, orth=?o2]
ADVB[category=?c, orth=(?o1,OR,?o2)] -> ADVB[category=?c, orth=?o1] OR ADVB[category=?c, orth=?o2]
DEG -> COMPADJ DEG
ADVB[+comp, category=adv, orth=?o] -> A[+makecomp] ADV[category=adv, orth=?o]

ABAR[category=?c, H=[orth=?o, category=?c], sem=?sem] -> A[category=?c, orth=?o, -position, sem=?sem]
ABAR[category=?c, H=[orth=?o, category=?c, mod=?adv]] -> ADVB[orth=?adv] A[category=?c, orth=?o, -position]
ABAR[category=?c, H=[orth=?o, category=?c, mod=?adv]] -> A[orth=?adv] A[category=?c, orth=?o, -position]
ABAR[category=?c, H=[orth=?o, category=?c, mod=?adv]] -> COUNT[orth=?adv] A[category=?c, orth=?o, -position]
#ABAR[category=?c, H=[orth=?o, category=?c, mod=?deg]] -> DEG[orth=?deg] A[category=?c, orth=?o, -position]

ORDELIM -> COMMA | OR | COMMA OR
ABARL[category=?c, H=(?h1+'OR'+?h2), sem=<?s1 | ?s2>] ->  ABAR[category=?c, H=?h1, sem=?s1] ORDELIM ABARL[category=?c, H=?h2, sem=?s2, -position]
ABARL[category=?c, H=?h, sem=?sem] ->  ABAR[category=?c, H=?h, sem=?sem]

ABAR[category=?c, H=[category=?c, orth=(?o1+'OR'+?h2)]] -> ABAR[category=?c, H=[orth=?o1]] OR WITHP[H=?h2]
ABAR[category=?c, H=[category=?c, orth=(?o1+'TO'+?o2)]] -> ABAR[category=?c, H=[orth=?o1]] TO ABAR[category=?c, H=[orth=?o2]]

#ABAR[category=?c, H=[category=?c, orth=?o1, position=?p]] -> ABAR[category=?c, H=[orth=?o1]] POSITION[posit=?p]

A[category=?c, orth=?o, H=[orth=?o, category=?c]] -> UNK[category=?c, orth=?o]

ABAR[category='prep', H=?p, mod=?m] -> DET PP[H=?p, mod=?m]
ABAR[category='prep', H=?p, mod=?m] -> PP[H=?p, pmod=?m]

ABAR[H=[compto=?h, comp=?o], +compar] -> A[+compar, orth=?o] THAN NP[H=?h]

ABAR -> NUM TIMES A[+compar] NP
ABAR[category=?c, H=?h] -> ABAR[category=?c, H=?h] PAREXP

AP[category=?c, H=?h] -> ABARL[category=?c, H=?h]

# AP[category=?c, H=?h] -> AP[category=?c, H=?h] PAREXP
AP[category=(?c+?d), H=(?h1+'AND'+?h2)] -> AP[category=?c, H=?h1] AND AP[category=?d, H=?h2]

WITHP[category=?c, H=[+presence, obj=?h]] -> WITH NP[category=?c, H=?h]
WITHP[category=?c, H=[+presence, obj=?h, clist=?cl]] -> WITH NP[category=?c, H=?h] CHARLIST[H=?cl]
WITHP[category=?c, H=[+presence, obj=?h, clist=?cl, mod=?m]] -> DEG[orth=?m] WITHP[category=?c, H=[+presence, obj=?h, clist=?cl, mod=?m]]
# WITHP[category=?c, H=[+presence, obj=?h, clist=?cl]] -> NP[category=?c, H=?h] CHRL[H=?cl]

WITHP[category=?c, H=[+presence, obj=(?h1+?h2)]] -> WITH NP[category=?c, H=?h1] AND  NP[category=?c, H=?h2]

OFP[category=?c, H=[prep=OF, obj=?h]] -> OF NP[category=?c, H=?h]

PP[H=[prep=?prep, obj=?n]] -> P[orth=?prep, -position] NP[H=?n]
PP[H=?h, mod=?deg] -> DEG[orth=?deg] PP[H=?h]

PAREXP -> PUNC[punc='('] CHRL PUNC[punc=')']
#PAREXP -> COMMA AP COMMA | PPAREXP

AND -> CONJ[conj=and]
OR  -> CONJ[conj=or]
CONJUNCTION -> CONJ[conj='and'] | CONJ[conj='but']

STAGE[stage=?o] -> 'in' A[category='life-stage', orth=?o] 'state'
STAGE[stage=?o] -> 'at' A[category='life-stage', orth=?o]
STAGE[stage=?o] -> A[+timing, orth=?o]

POSITION[posit=?o] -> P[prep=?p, orth=?o, +position]
POSITION[posit=?h] -> P[prep=?prep, +position] NP[H=?n]
POSITION[posit=?o] -> A[+position, orth=?o]

SUBSET[stage=(?a+?n)] -> 'in' ABARL[orth=?a] N[orth=?n]

ANAPH[category=?o] -> 'the' A[orth=?o] 'ones'
ANAPH[category=?o] -> 'the' A[orth=?o] 'one'


